import datatypes from "Banking.data" as banking

/**
 * Person context
 */
context Person {
    // Data structure representing a person that has several banking accounts
    structure Person<entity> {
        string firstname,
        string lastname,
        string gender = "unknown",
        date birthday,
        immutable int daysToNextBirthday<neverEmpty>,
        banking::v1.Banking.Accounts accounts,

        function string getFullName<sideEffectFree>,
        procedure setFirstnameLastnameFromFullName(string fullname)
    }
}

/**
 * Student context
 */
context Student {
    // Data structure representing a student, who is a person, and may attend several courses at
    // university
    structure Student<entity> extends Person.Person {
        int studentNumber<identifier>,
        int numberOfTerms,
        Courses courses,
        hide boolean paidTuitionFeeForCurrentTerm
    }

    // Data structure representing a university's courses
    structure Course<valueObject> {
        int number,
        string name,
        boolean offeredInWinterTerm,
        boolean offeredInSummerTerm
    }

    // List type to hold a sequence of Course instances
    list Courses {Course course}
}